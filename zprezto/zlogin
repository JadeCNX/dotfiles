#
# Executes commands at login post-zshrc.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

# Execute code that does not affect the current session in the background.
{
  # Compile the completion dump to increase startup speed.
  zcompdump="${XDG_CACHE_HOME:-$HOME/.cache}/prezto/zcompdump"
  if [[ -s "$zcompdump" && (! -s "${zcompdump}.zwc" || "$zcompdump" -nt "${zcompdump}.zwc") ]]; then
    zcompile "$zcompdump"
  fi
} &!

# Execute code only if STDERR is bound to a TTY.
if [[ -o INTERACTIVE && -t 2 ]]; then

  # Print a random, hopefully interesting, adage.
  if (( $+commands[fortune] )); then
    if (( $+commands[pokemonsay] )); then
      fortune -s | pokemonsay -n -t
    elif (( $+commands[lolcat] )) && (( $+commands[cowsay] )); then
      fortune -s | cowsay -f `echo "${$(cowsay -l | tail -n +2)// /\n}" | sort -R | tail -1` | lolcat
    elif (( $+commands[ponysay] )); then
      fortune -s | ponysay
    else
      fortune -s
    fi
    print
  fi

fi >&2
